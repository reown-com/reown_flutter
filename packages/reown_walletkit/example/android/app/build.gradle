plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('secrets.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.walletconnect.flutterwallet"
    compileSdk flutter.compileSdkVersion
    ndkVersion "27.0.12077973"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlin {
        jvmToolchain(17)
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.walletconnect.flutterwallet"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-build-configuration.
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion 33
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    // Specifies one flavor dimension.
    flavorDimensions = ["version"]

    productFlavors {
        internal {
            dimension "version"
            applicationIdSuffix ".internal"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_internal",
                    applicationLabel: "FL WalletKit",
            ]
        }
        production {
            dimension "version"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    applicationLabel: "FL WalletKit",
            ]
        }
    }

    signingConfigs {
        debug {
            def wcFilePath = keystoreProperties['WC_FILENAME']
            if (wcFilePath) {
                storeFile file(wcFilePath)
                storePassword keystoreProperties['WC_STORE_PASSWORD']
                keyAlias keystoreProperties['WC_KEYSTORE_ALIAS']
                keyPassword keystoreProperties['WC_KEY_PASSWORD']
            } else {
                println "⚠️  WC_FILENAME not set — skipping custom signing config (will use default debug keystore)"
            }
        }
    }

    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig signingConfigs.debug
        }
    }

    packaging {
        resources {
            excludes += [
                    "META-INF/versions/9/OSGI-INF/MANIFEST.MF"
            ]
        }
    }

    namespace 'com.walletconnect.flutterwallet'
}

flutter {
    source '../..'
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.3'
}
